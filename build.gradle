
buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:2.1.2'
    classpath 'com.f2prateek.checkstyle:checkstyle:1.0.1'
    classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
  }
}

ext {
  bintrayRepo = 'Maven'
  bintrayName = 'clevertap-segment-integration'
  org = 'clevertap'

  publishedGroupId = 'com.clevertap.android'
  libraryName = 'clevertap-segment-integration'
  artifact = 'clevertap-segment-android'

  libraryDescription = 'CleverTap Integration for Segment Android Analytics'

  siteUrl = 'https://github.com/CleverTap/clevertap-segment-android'
  gitUrl = 'https://github.com/CleverTap/clevertap-segment-android.git'

  libraryVersion = '1.0.6'

  developerId = 'clevertap'
  developerName = 'CleverTap'
  developerEmail = 'support@clevertap.com'

  licenseName = 'The MIT License (MIT)'
  licenseUrl = 'http://opensource.org/licenses/MIT'
  allLicenses = ["MIT"]
}

version = libraryVersion
group = publishedGroupId

apply plugin: 'com.android.library'
apply plugin: 'checkstyle'
apply plugin: 'com.f2prateek.checkstyle'

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'


android {
  compileSdkVersion 23
  buildToolsVersion '23.0.3'

  defaultConfig {
    minSdkVersion 14
    targetSdkVersion 23
    compileSdkVersion 23
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

  testOptions {
    unitTests.returnDefaultValues = true
  }
}

checkstyle {
  configFile rootProject.file('gradle/checkstyle.xml')
}

// Bintray
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
  user = properties.getProperty("bintray.user")
  key = properties.getProperty("bintray.apikey")

  configurations = ['archives']
  pkg {
    repo = bintrayRepo
    name = bintrayName
    userOrg = org
    desc = libraryDescription
    websiteUrl = siteUrl
    vcsUrl = gitUrl
    licenses = allLicenses
    publish = true
    publicDownloadNumbers = true
    version {
      desc = libraryDescription
      gpg {
        sign = false //Determines whether to GPG sign the files. The default is false
        //Optional. The passphrase for GPG signing'
      }
    }
  }
}

install {
  repositories.mavenInstaller {
    // This generates POM.xml with proper parameters
    pom {
      project {
        packaging 'aar'
        groupId publishedGroupId
        artifactId artifact

        // Add your description here
        name libraryName
        description libraryDescription
        url siteUrl

        // Set your license
        licenses {
          license {
            name licenseName
            url licenseUrl
          }
        }
        developers {
          developer {
            id developerId
            name developerName
            email developerEmail
          }
        }
        scm {
          connection gitUrl
          developerConnection gitUrl
          url siteUrl

        }
      }
    }
  }
}

dependencies {
  repositories {
    mavenCentral()
    jcenter()
  }

  provided 'com.segment.analytics.android:analytics:4.0.0'

  compile 'com.clevertap.android:clevertap-android-sdk:2.2.0'
  compile 'com.android.support:support-v4:23.4.0'
  compile 'com.google.android.gms:play-services-gcm:9.0.2'

  testCompile 'junit:junit:4.12'
  testCompile('org.robolectric:robolectric:3.0') {
    exclude group: 'commons-logging', module: 'commons-logging'
    exclude group: 'org.apache.httpcomponents', module: 'httpclient'
  }

  testCompile 'com.segment.analytics.android:analytics-tests:4.0.0'

  testCompile 'org.assertj:assertj-core:1.7.1'

  testCompile 'org.mockito:mockito-core:1.10.19'

  testCompile 'org.powermock:powermock:1.6.2'
  testCompile 'org.powermock:powermock-module-junit4:1.6.2'
  testCompile 'org.powermock:powermock-module-junit4-rule:1.6.2'
  testCompile 'org.powermock:powermock-api-mockito:1.6.2'
  testCompile 'org.powermock:powermock-classloading-xstream:1.6.2'

  androidTestCompile 'com.segment.analytics.android:analytics-tests:4.0.0'
  androidTestCompile 'com.android.support:support-annotations:23.4.0'
  androidTestCompile 'com.android.support.test:runner:0.4.1'
  androidTestCompile 'com.android.support.test:rules:0.4.1'
  androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
}

task sourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier = 'sources'
}

artifacts {
  archives sourcesJar
}
